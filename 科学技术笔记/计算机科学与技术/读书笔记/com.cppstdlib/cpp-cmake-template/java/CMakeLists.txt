file(GLOB_RECURSE MYLIB_SWIG_FILES ${CMAKE_CURRENT_SOURCE_DIR} *.i)

find_package(SWIG REQUIRED)
find_package(Java REQUIRED)
find_package(JNI REQUIRED)
include(UseJava)
include(UseSWIG)

set(MYLIB_PACKAGE_NAME "org.acme")
string(REPLACE "." "/" MYLIB_PACKAGE_DIR ${MYLIB_PACKAGE_NAME})

set(CMAKE_SWIG_FLAGS -package ${MYLIB_PACKAGE_NAME})
set(CMAKE_SWIG_OUTDIR ${CMAKE_CURRENT_BINARY_DIR}/src/${MYLIB_PACKAGE_DIR})

set_source_files_properties(${MYLIB_SWIG_FILES} PROPERTIES CPLUSPLUS ON)

include_directories(
  ${PROJECT_SOURCE_DIR}/include
  ${JNI_INCLUDE_DIRS}
)

swig_add_module(MyLibCPP java ${MYLIB_SWIG_FILES})

swig_link_libraries(MyLibCPP MyLib)

set(CMAKE_JAVA_TARGET_VERSION ${MYLIB_VERSION})
set(CMAKE_JNI_TARGET TRUE)
add_jar(MyLibJava ${CMAKE_SWIG_OUTDIR}/*.java)
add_dependencies(MyLibJava MyLibCPP)

create_javadoc(javadoc
  PACKAGES ${MYLIB_PACKAGE_NAME}
  SOURCEPATH ${CMAKE_CURRENT_BINARY_DIR}/src
  CLASSPATH ${CMAKE_JAVA_INCLUDE_PATH}
  INSTALLPATH ${CMAKE_CURRENT_BINARY_DIR}
  WINDOWTITLE ${MYLIB_NAME}
  DOCTITLE "<h1>${MYLIB_NAME}</h1>"
  AUTHOR TRUE
  USE TRUE
  VERSION TRUE
)

install_jar(MyLibJava java/lib)
install(FILES ${CMAKE_CURRENT_BINARY_DIR}/${CMAKE_SHARED_LIBRARY_PREFIX}MyLibCPP${CMAKE_SHARED_LIBRARY_SUFFIX}
  DESTINATION java/lib
)
# TODO OP retrieve Doxygen doc in Javadoc?
install(DIRECTORY ${CMAKE_CURRENT_BINARY_DIR}/javadoc
  DESTINATION java
)
